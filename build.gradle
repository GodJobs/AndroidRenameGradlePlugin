group 'market.dreamer.build.android'
version '0.0.5'

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.5

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.android.tools.build:gradle:1.2.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    baseName = 'android-rename'
}

javadocJar {
    baseName = 'android-rename'
}

sourcesJar {
    baseName = 'android-rename'
}

artifacts {
    archives javadocJar, sourcesJar
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    apply plugin: 'maven'
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'AndroidRenameGradlePlugin'
                    url 'https://github.com/GodJobs/AndroidRenameGradlePlugin'

                    scm {
                        url 'scm:git@github.com:GodJobs/AndroidRenameGradlePlugin.git'
                        connection 'scm:git@github.com:GodJobs/AndroidRenameGradlePlugin.git'
                        developerConnection 'git@github.com:GodJobs/AndroidRenameGradlePlugin.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://github.com/GodJobs/AndroidRenameGradlePlugin/blob/master/LICENSE'
                            distribution 'GodJobs'
                        }
                    }

                    developers {
                        developer {
                            id 'GodJobs'
                            name 'GodJobs'
                            email 'marz.public@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

